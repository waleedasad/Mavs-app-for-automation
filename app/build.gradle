apply plugin: 'com.android.application'


def isRunningOnTravisAndIsNotPRBuild = System.getenv("CI") == "true" && file('../api-5888530012451153317-910289-17cb4835c79c.json').exists()

if(isRunningOnTravisAndIsNotPRBuild) {
    apply plugin: 'com.github.triplet.play'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0' //version required
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.0"
    defaultConfig {
        applicationId "com.pumpjackdataworks.testapp"
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    if (isRunningOnTravisAndIsNotPRBuild) {
        signingConfigs.release.storeFile = file("../androidTestApp.jks")
        signingConfigs.release.storePassword = System.getenv("STORE_PASS")
        signingConfigs.release.keyAlias = System.getenv("KEY_ALIAS")
        signingConfigs.release.keyPassword = System.getenv("KEY_PASS")
    }
}
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
//    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}

if (isRunningOnTravisAndIsNotPRBuild) {
    play {
        track = "alpha"
        userFraction = 1
        serviceAccountCredentials = file("./api-5888530012451153317-910289-17cb4835c79c.json")
        resolutionStrategy = "auto"
        outputProcessor { // this: ApkVariantOutput
            versionNameOverride = "$versionNameOverride.$versionCode"
        }
    }
}